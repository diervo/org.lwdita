apply plugin: "java"
apply plugin: "maven"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.elovirta.dita'
version = "2.0.1"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: "org.dita-ot", name: "dost", version: "[2.1,)"
    compile group: "com.vladsch.flexmark", name: "flexmark-all", version: "0.26.4"
    compile group: 'nu.validator.htmlparser', name: 'htmlparser', version: '1.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'xmlunit', name: 'xmlunit', version: '1.6'
}

task dist(type: Zip) {
    from(configurations.runtime.allArtifacts.files) {
        into "lib"
    }
    from(configurations.runtime) {
        include { details -> details.file.name.contains('flexmark') }
        exclude { details -> details.file.name.contains('flexmark-pdf') }
        include { details -> details.file.name.contains('autolink') }
        include { details -> details.file.name.contains('htmlparser') }
        into "lib"
    }
    from("src/main") {
        include "plugin.xml"
        expand(
            id: project.name,
            version: project.version
        )
    }
    from("src/main") {
        include "build-*_template.xml"
        include "conductor.xml"
        // Template generated files
        exclude "build.xml"
    }
    from("src/main/resources") {
        include "*.xsl"
        exclude "specialize.xsl"
        // Template generated files
        exclude "dita2markdown.xsl"
        exclude "map2markdown-cover.xsl"
        rename '(.*)', 'xsl/$1'
    }
    from("licenses") {
        include "*.txt"
        into "lib"
    }
    from(".") {
        include "LICENSE"
    }
    from(".") {
        include "NOTICES"
        expand(
            year: new Date().format('yyyy')
        )
    }
    archiveName "${project.name}-${project.version}.zip"
}
